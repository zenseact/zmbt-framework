# (c) Copyright 2024-2025 Zenseact AB
load("//zmbt-framework/backends/cxx:config.bzl", "ZMBT_COPTS", "BOOST_LIBS")
load("@rules_pkg//pkg:mappings.bzl", "pkg_files", "strip_prefix")
load("@rules_pkg//pkg:deb.bzl", "pkg_deb")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")


config_setting(
    name = "debug",
    values = {
        "compilation_mode": "dbg",
    },
)

cc_library(
    name = "zmbt",
    visibility = ["//visibility:public"],
    includes = ["include/"],
    hdrs = [
        "include/zenseact-mbt.hpp",
    ],
    srcs = glob(
        [
            "src/**/*.cpp",
            "include/**/*.hpp",
        ],
        exclude = [
            "src/main*.cpp",
            "src/appconfig/*.cpp",
            "include/appconfig/**/*.hpp",
        ],
    ),
    deps = BOOST_LIBS,
    copts = ZMBT_COPTS + select({
        "//conditions:default": [],
        ":debug": [
            "-g",
            "-O0",
        ],
    }),
)


cc_library(
    name = "zmbt_main_boost",
    visibility = ["//visibility:public"],
    includes = ["include/"],
    srcs = [
        "src/main_boost.cpp",
    ],
    deps = [
        ":zmbt",
        "@boost//:test.so",
    ],
    copts = ZMBT_COPTS,
)

cc_library(
    name = "zmbt_main_gtest",
    visibility = ["//visibility:public"],
    includes = ["include/"],
    srcs = [
        "src/main_gtest.cpp",
    ],
    deps = [
        ":zmbt",
        "@googletest//:gtest",
    ],
    copts = ZMBT_COPTS,
)

filegroup(
    name = "zmbt_headers",
    visibility = [":__pkg__"],
    srcs = glob(
        [
            "include/**/*.hpp",
        ],
        exclude = [
            "include/appconfig/*",
        ],
    ),
)

pkg_files(
    name = "package_header_files",
    visibility = [":__pkg__"],
    srcs = [
        ":zmbt_headers",
    ],
    strip_prefix = strip_prefix.from_pkg()
)

pkg_files(
    name = "package_bin_files",
    visibility = [":__pkg__"],
    srcs = [
        ":zmbt",
        ":zmbt_main_gtest",
        ":zmbt_main_boost",
    ],
    prefix = "lib",
    strip_prefix = strip_prefix.from_pkg()
)

pkg_tar(
    name = "zmbt_cxx_tar",
    extension = "tar.gz",
    tags = ["manual"],
    srcs = [
        ":package_header_files",
        ":package_bin_files",
    ],
)
